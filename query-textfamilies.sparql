PREFIX xyz: <http://sparql.xyz/facade-x/data/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX fx: <http://sparql.xyz/facade-x/ns/>
PREFIX mm:  <http://w3id.org/polifonia/ontology/music-meta/>
PREFIX mp:  <http://w3id.org/polifonia/ontology/music-projection/>
PREFIX mr:  <http://w3id.org/polifonia/ontology/music-representation/>
PREFIX core:  <http://w3id.org/polifonia/ontology/core/>
PREFIX tunes:  <http://w3id.org/polifonia/ontology/tunes/>
PREFIX src: <http://w3id.org/polifonia/ontology/source/>
PREFIX pon-resource: <http://w3id.org/polifonia/resource/tunes/>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>

CONSTRUCT {
    ?textfamily_iri a core:Collection, tunes:TextFamily ;
        core:isDefinedBy <pon-resource:CollectionConcept/TextFamily> ;
        core:id ?textfamily_id ;  # add to ontology
        core:name ?textfamily_name ;
        core:description ?textfamily_description_url .

    <pon-resource:CollectionConcept/TextFamily> a core:CollectionConcept ;
        rdfs:label "Melodies that belong to the same text family" ; #how to do this properly?
        core:hasMember ?textfamily_iri .

}


WHERE {
    SERVICE <x-sparql-anything:> {

        fx:properties fx:location ?_filepath ;
            fx:media-type  "application/json" .

        ?base_root a fx:root ;
            ?tf_slot ?tf_base .

        ?tf_base 
            xyz:textfamily_iri ?textfamily_iri_temp ;
            xyz:textfamily_id ?textfamily_id ;
            xyz:textfamily_name ?textfamily_name ;
            xyz:textfamily_url ?textfamily_description_url .

        BIND(IRI(?textfamily_iri_temp) as ?textfamily_iri)

    }
}

